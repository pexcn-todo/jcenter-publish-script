apply plugin: 'com.jfrog.bintray'

version = rootProject.ext.libraryVersionName

if (project.hasProperty("android")) {
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }
} else {
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

artifacts {
    archives sourcesJar
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    override = true
    pkg {
        repo = 'maven'
        name = rootProject.ext.libraryName
        desc = rootProject.ext.libraryDescription
        websiteUrl = rootProject.ext.siteUrl
        vcsUrl = rootProject.ext.gitUrl
        licenses = rootProject.ext.licenses
        publish = true
        publicDownloadNumbers = true
        version {
            desc = rootProject.ext.libraryDescription
            gpg {
                sign = false
            }
        }
    }
}
